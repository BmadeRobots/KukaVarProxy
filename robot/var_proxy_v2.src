&ACCESS RVP
&REL 159
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\ExpertVorgabe
DEF var_proxy_v2( )
ghCommand.motiontype = 0
ghCommand.speed = 10

WAIT SEC 1.0 ;

;FOLD INI

  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)


LOOP


$ADVANCE =1;

SWITCH ghCommand.motionType
    CASE 1
        msgnotify("motion type 1 - jnt ")
       ;DO NOT USE "CHANGE" on tp for the next line; See the detail under open fold
;FOLD PTP jntPos Vel=100 % PDAT3 Tool[1]:DFM24_SPIKE Base[1]:DFM24_BASE;%{PE}%R 8.3.38,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:jntPos, 3:, 5:100, 7:PDAT3
$BWDSTART=FALSE
PDAT_ACT=PPDAT3
FDAT_ACT=FjntPos
BAS(#PTP_PARAMS,ghCommand.speed)
PTP ghCommand.jntPos 
;ENDFOLD
      ghCommand.motionType = 0

    CASE 2
        msgnotify("motion type 2 - linear ")
        PTP $AXIS_ACT
        ghMotionBuf = ghCommand.targetPos
        IF (VARSTATE("ghCommand.targetPos.S")<>#INITIALIZED) THEN
           ghMotionBuf.S = $POS_ACT.S            
           ghMotionBuf.T = $POS_ACT.T

        ENDIF 
        ; DO NOT USE "CHANGE" on tp for the next line; See the detail under open fold
;FOLD LIN p7 Vel=2 m/s CPDAT2 Tool[1]:DFM24_SPIKE Base[1]:DFM24_BASE;%{PE}%R 8.3.38,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:p7, 3:, 5:2, 7:CPDAT2
$BWDSTART=FALSE
LDAT_ACT=LCPDAT2
FDAT_ACT=Fp7
BAS(#CP_PARAMS,ghCommand.speed)
LIN ghMotionBuf

;ENDFOLD
       ghCommand.motionType = 0
    CASE 3
         msgnotify("cmd type 3 - setting do")
         $OUT[ghCommand.do_address] = ghCommand.do_value 
         ghCommand.motionType=0
    DEFAULT    
        msgnotify("waiting next cmd");
        WAIT SEC 1.0;
ENDSWITCH
ENDLOOP

END
